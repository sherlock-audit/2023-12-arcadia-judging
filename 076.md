Main Yellow Jaguar

medium

# A malicious passive lender would legitimately be able to avoid a loss by **front-running** the TX of the Tranche#`lock()`, which is internally called via the Liquidator#`bid()` when a bad debt happen

## Summary
A malicious passive lender would legitimately be able to avoid a loss by **front-running** the TX of the Tranche#`lock()`, which is internally called via the Liquidator#`bid()` when a bad debt happen.


## Vulnerability Detail

Within the Liquidator#`bid()`, the Liquidator#`_settleAuction()` would be called - if all the debt is not repaid (`earlyTerminate != true`) and a given `endAuction_` is `true` like this:
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/Liquidator.sol#L315
```solidity
    function bid(address account, uint256[] memory askedAssetAmounts, bool endAuction_) external nonReentrant {
        AuctionInformation storage auctionInformation_ = auctionInformation[account];
        ...
        // If all the debt is repaid, the auction must be ended, even if the bidder did not set endAuction to true.
        if (earlyTerminate) {
            // Stop the auction, no need to do a health check for the account since it has no debt anymore.
            _endAuction(account);
        }
        // If not all debt is repaid, the bidder can still earn a termination incentive by ending the auction
        // if one of the conditions to end the auction is met.
        // "_endAuction()" will silently fail without reverting, if the auction was not successfully ended.
        else if (endAuction_) {
            if (_settleAuction(account, auctionInformation_)) _endAuction(account); ///<---------- @audit
        }
```

Within the Liquidator#`_settleAuction()`, the LendingPool#`settleLiquidationUnhappyFlow()` would be called - when all collateral is sold (`collateralValue == 0`) or the auction did not end within the cutoffTime (`block.timestamp > auctionInformation_.cutoffTimeStamp`) like this:
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/Liquidator.sol#L455
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/Liquidator.sol#L458
```solidity
    function _settleAuction(address account, AuctionInformation storage auctionInformation_)
        internal
        returns (bool success)
    {
        ...
        // Check the different conditions to end the auction.
        if (collateralValue >= usedMargin || usedMargin == minimumMargin) {
            ...
        } else if (collateralValue == 0) {
            // Unhappy flow: All collateral is sold.
            ILendingPool(creditor).settleLiquidationUnhappyFlow(account, startDebt, minimumMargin, msg.sender);  ///<------- @audit
        } else if (block.timestamp > auctionInformation_.cutoffTimeStamp) {
            // Unhappy flow: Auction did not end within the cutoffTime.
            ILendingPool(creditor).settleLiquidationUnhappyFlow(account, startDebt, minimumMargin, msg.sender);  ///<------- @audit
            ...
        } else {
            ...
```

Within the LendingPool#`settleLiquidationUnhappyFlow()`, the LendingPool#`_processDefault()` would be called - if the `openDebt` would be larger than the sum of the `terminationReward` and the `liquidationPenalty` (`openDebt > terminationReward + liquidationPenalty`), which means that a **bad debt** would happen like this:
(NOTE：The **bad debt** would be calculated based on [`openDebt - terminationReward - liquidationPenalty`](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/LendingPool.sol#L1001))
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/LendingPool.sol#L997
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/LendingPool.sol#L1001
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/LendingPool.sol#L1005
```solidity
    function settleLiquidationUnhappyFlow(
        address account,
        uint256 startDebt,
        uint256 minimumMargin_,
        address terminator
    ) external whenLiquidationNotPaused onlyLiquidator processInterests {
        (uint256 initiationReward, uint256 terminationReward, uint256 liquidationPenalty) =
            _calculateRewards(startDebt, minimumMargin_);

        // Any remaining debt that was not recovered during the auction must be written off.
        // Depending on the size of the remaining debt, different stakeholders will be impacted.
        uint256 debtShares = balanceOf[account];
        uint256 openDebt = convertToAssets(debtShares);
        uint256 badDebt;
        if (openDebt > terminationReward + liquidationPenalty) { ///<------------------- @audit
            // "openDebt" is bigger than pending liquidation incentives.
            // No incentives will be paid out, and a default event is triggered.
            unchecked {
                badDebt = openDebt - terminationReward - liquidationPenalty;
            }

            totalRealisedLiquidity = uint128(totalRealisedLiquidity - badDebt);
            _processDefault(badDebt); ///<------------------- @audit
        } else {
           ...
```

Within the LendingPool#`_processDefault()`, `0` would be stored into the liquidity balance of the `tranche` (`realisedLiquidityOf[tranche] = 0`), which means that **all** liquidity balance-provided by the passive lenders is spent for covering the loss due to the bad debt and therefore these passive lenders face the loss of the liquidity that they provided to the `tranche`.
Then, the Tranche#`lock()` would be called like this:
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/LendingPool.sol#L1082
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/LendingPool.sol#L1087
```solidity
    function _processDefault(uint256 badDebt) internal {
        address tranche;
        uint256 maxBurnable;
        uint256 length = tranches.length;
        for (uint256 i = length; i > 0;) {
            ...
            tranche = tranches[i];
            maxBurnable = realisedLiquidityOf[tranche];
            if (badDebt < maxBurnable) {
                ...
            } else { /// @audit info - if (badDebt >= maxBurnable) 
                // Unhappy flow, should never occur in practice!
                // badDebt is bigger than the balance of most junior Tranche -> tranche is completely wiped out
                // and temporarily locked (no new deposits or withdraws possible).
                // DAO or insurance might refund (Part of) the losses, and add Tranche back.
                realisedLiquidityOf[tranche] = 0; ///<------------------- @audit
                _popTranche(i, tranche);
                unchecked {
                    badDebt -= maxBurnable;
                }
                ITranche(tranche).lock(); ///<------------------- @audit
                ...
            }
            ...
```

Within the Tranche#`lock()`, `true` would be stored into the `locked` storage:
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/Tranche.sol#L117
```solidity
    /**
     * @notice Locks the tranche in case all liquidity of the tranche is written off due to bad debt.
     * @dev This function can only be called by the Lending Pool and is triggered exclusively during a severe default event.
     */
    function lock() external {
        if (msg.sender != address(LENDING_POOL)) revert TrancheErrors.Unauthorized();

        emit LockSet(locked = true);
        emit AuctionInProgressSet(auctionInProgress = false);
    }
```

As we can see the liquidation process above, when a bad debt would happen, the Tranche#`lock()` would be called via the Liquidator#`_processDefault()`. 

Once the Tranche contract would be `locked` via the Tranche#`lock()`, a passive lender can **not** call the following functions in the Tranche contract - because of the `notLocked()` modifier-attached on these following functions:
- Tranche#`deposit()`
   https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/Tranche.sol#L159

- Tranche#`withdraw()`
   https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/Tranche.sol#L211

Once the default process would be completed, the owner would call the Tranche#`unlock()`.
Then, once the owner would call the Tranche#`unlock()`, `false` would be stored into the `locked` storage like below and therefore the **passive lender** can call the following functions above again:
https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/Tranche.sol#L127
```solidity
    /**
     * @notice Unlocks the tranche.
     * @dev Only the Owner can call this function, since tranches are locked due to complete defaults.
     * This function will only be called to partially refund existing share-holders after a default.
     */
    function unLock() external onlyOwner {
        emit LockSet(locked = false); ///<---------- @audit
    }
```

All liquidity of the (junior) `tranche` (`realisedLiquidityOf[tranche]`), which is provided by the passive lenders, is supposed to be locked to be spent for covering a loss - if a **bad debt** would happen. 

However, since a passive lender can monitor the TX of the Liquidator#`bid()` in the mempool, the passive lender can predict whether or not a bad debt is likely to happen.
Hence, when a bidder would call the Liquidator#`bid()` and the malicious passive lender thought that a bad debt is likely to be happened by the TX of the Liquidator#`bid()`, the (malicious) passive lender can **front-run** it with calling the [Tranche#`withdraw()`](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/Tranche.sol#L211) to avoid a loss.


## PoC (Scenario)
**Assumption:**
Let's say there are 5 actors: 
- Alice is a liquidator.
- Bob is a malicious passive lender.
- Paul is the owner of the Tranche contract (`tranche`).
- Ken is a borrower (Account Owner).
- Alen is a bidder.

**Scenario:**
- 1/ Ken's Account would be moved to a liquidatable status - due to that her collateral value to fall below the required liquidation threshold.
- 2/ Alice would call the Liquidator#`liquidateAccount()` to liquidate the Ken's Account.
- 3/ Alen would call the Liquidator#`bid()`.
- 4/ Bob would monitor the TX of the step 3/ and he thought that the **bad debt** is likely to happen.
   - Hence, he **front-run** the TX of the step 3/ with calling the [Tranche#`withdraw()`](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/Tranche.sol#L208-L230).
   
- 5/ The Alice's TX (step 2/) would be executed first.
- 6/ The Bob's TX (step 4/) would be executed second.
  - Bob would be successful to withdraw his funds from the `tranche`. 
- 7/ The Alen's TX (step 3/) would be executed third. 
   - At this point, a bad debt happen.
   - Since a bad debt happen, the `tranche` would be `locked` via the Tranche#`lock()`, which is internally called through the Liquidator#`_processDefault()`. 
     (NOTE：The Liquidator#`_processDefault()` above would internally be called via the Liquidator#`bid()`)
- 8/ Paul would call the [Tranche#`unlock()`](https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/Tranche.sol#L126-L128).
  - Now, the `tranche` would be `unlocked`. 

As you can see the scenario above, since the Bob's TX was executed (6/) before the Alen's TX was executed (7/), Bob would be successful to withdraw their funds - before the `tranche` would be locked.

Bob is a passive lender of the (junior) `tranche` and therefore his funds was supposed to be spent for covering a loss of the bad debt - when the step 7/. 
However, Bob would legitimately be successful to avoid a loss by **front-running**.

## Impact
A malicious passive lender would legitimately be able to avoid a loss by **front-running** the internal TX of the Tranche#`lock()`, which is internally called via the Liquidator#`bid()` when a bad debt happen.

## Code Snippet
- https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/LendingPool.sol#L1087
- https://github.com/sherlock-audit/2023-12-arcadia/blob/main/lending-v2/src/Tranche.sol#L117

## Tool used
- Manual Review

## Recommendation
Consider calling the Tranche#`lock()` in the Liquidator#`liquidateAccount()` - instead of calling it in the Liquidator#`_processDefault()`.